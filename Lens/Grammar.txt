main                = { stmt } EOF
stmt                = using | recorddef | typedef | funcdef | ( local_stmt NL )
using               = "using" namespace NL
namespace           = identifier { "." identifier }
recorddef           = "record" identifier NL recorddef_stmt { recorddef_stmt }
recorddef_stmt      = INDENT identifier ":" type NL
typedef             = "type" identifier NL typedef_stmt { typedef_stmt }
typedef_stmt        = INDENT "|" identifier [ "of" type ] NL
funcdef             = "fun" identifier func_params "->" block
func_params         = { identifier ":" [ ( "ref" | "out" ) ] type }
block               = NL block_line { block_line } | local_stmt
block_line          = INDENT local_stmt NL
type                = namespace [ ( type_params | { "[]" } ) ]
local_stmt          = var_decl_expr | assign_expr | expr
var_decl_expr       = "let" | "var" identifier "=" expr
assign_expr         = lvalue "=" expr
lvalue              = [ type "::" ] identifier { accessor_expr } | atomar_expr accessor_expr { accessor_expr }
atomar_expr         = literal | type_operator_expr | "(" expr ")"
accessor_expr       = "." identifier | "[" line_expr "]"
type_params         = "<" type { "," type } ">"
expr                = block_expr | line_expr
block_expr          = if_expr | while_expr | try_expr | lambda_expr
if_expr             = "if" "(" line_expr ")" block [ "else" block ]
while_expr          = "while" "(" line_expr ")" block
try_expr            = "try" block catch_expr { catch_expr }
catch_expr          = "catch" [ "(" type identifier ")" ] block
lambda_expr         = [ "(" func_params ")" ] "->" block
line_expr           = line_expr_1 [ "as" type ]
line_expr_1         = line_expr_2 { sign_1 line_expr_2 }
sign_1              = "&&" | "||" | "^^"
line_expr_2         = line_expr_3 { sign_2 line_expr_3 }
sign_2              = "==" | "<>" | "<" | ">" | "<=" | ">="
line_expr_3         = [ "not" | "-" ] line_expr_4 { sign_3 line_expr_4 }
sign_3              = "+" | "-"
line_expr_4         = line_expr_5 { sign_4 line_expr_5 }
sign_4              = "*" | "/" | "%"
line_expr_5         = line_expr_6 { "**" line_expr_6 }
line_expr_6         = line_expr_7 { "[" expr "]" }
line_expr_7         = new_expr | value_expr | invoke_expr
new_expr            = "new" ( new_array_expr | new_tuple_expr | new_list_expr | new_dict_expr | new_obj_expr )
new_array_expr      = "[" enumeration_expr "]"
new_tuple_expr      = "(" enumeration_expr ")"
new_list_expr       = "<" enumeration_expr ">"
new_dict_expr       = "{" dict_entry_expr { ";" dict_entry_expr } "}"
dict_entry_expr     = value_expr "=>" value_expr
new_obj_expr        = type [ invoke_list ]
enumeration_expr    = line_expr { ";" line_expr }
invoke_expr         = value_expr invoke_list
invoke_list         = NL { IDENT "<|" expr NL } | { value_expr }
value_expr          = atomar_expr | lvalue
type_operator_expr  = ( "typeof" | "default" ) "(" type ")"
literal             = "()" | "null" | "true" | "false" | string | double | int

string              = ...
int                 = [0-9]+
double              = [0-9]+\.[0-9]+
identifier          = [a-zA-Z_][0-9a-zA-Z_]*

NL                  = "\n"
INDENT              = { " " }
EOF                 = \EOF
