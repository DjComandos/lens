main                = { stmt } EOF
stmt                = using | recorddef | typedef | funcdef | ( local_stmt NL )
using               = "using" namespace NL
namespace           = identifier { "::" identifier }
recorddef           = "record" identifier NL recorddef_stmt { recorddef_stmt }
recorddef_stmt      = INDENT identifier ":" type NL
typedef             = "type" identifier NL typedef_stmt { typedef_stmt }
typedef_stmt        = INDENT "|" identifier [ "of" type ] NL
funcdef             = "fun" identifier func_params "->" block
func_params         = { identifier ":" [ ( "ref" | "out" ) ] type }
block               = NL block_line { block_line } | line_expr
block_line          = INDENT local_stmt NL
type                = [ namespace "::" ] identifier [ ( { "[]" } | "<" type ">" ) ]
local_stmt          = assign_expr | expr
assign_expr         = ( [ "let" | "var" ] identifier | rvalue ) "=" expr
rvalue              = ( type | "(" line_expr ")" ) accessor_expr { accessor_expr }
accessor_expr       = "." identifier | "[" line_expr "]"
expr                = block_expr | line_expr
block_expr          = if_expr | while_expr | try_expr | lambda_expr
if_expr             = "if" "(" line_expr ")" block [ "else" block ]
while_expr          = "while" "(" line_expr ")" block
try_expr            = "try" block catch_expr { catch_expr }
catch_expr          = "catch" [ "(" type identifier ")" ] block
lambda_expr         = [ "(" func_params ")" ] "->" block
line_expr           = line_expr_1 [ "as" type ]
line_expr_1         = line_expr_2 { sign_1 line_expr_2 }
sign_1              = "&&" | "||" | "^^"
line_expr_2         = line_expr_3 { sign_2 line_expr_3 }
sign_2              = "==" | "<>" | "<" | ">" | "<=" | ">="
line_expr_3         = [ "not" | "-" ] line_expr_4 { sign_3 line_expr_4 }
sign_3              = "+" | "-"
line_expr_4         = line_expr_5 { sign_4 line_expr_5 }
sign_4              = "*" | "/" | "%"
line_expr_5         = line_expr_6 { "**" line_expr_6 }
line_expr_6         = line_expr_7 { "[" expr "]" }
line_expr_7         = new_expr | invoke_expr
new_expr            = "new" ( new_array_expr | new_tuple_expr | new_obj_expr )
new_array_expr      = "[" enumeration_expr "]"
new_tuple_expr      = "(" enumeration_expr ")"
new_obj_expr        = type [ invoke_expr ]
enumeration_expr    = line_expr { ";" line_expr }
invoke_expr         = value_expr [ invoke_list ]
invoke_list         = { value_expr } | ( { NL "<|" value_expr } NL )
value_expr          = type { typeof_expr | accessor_expr } | literal | "(" expr ")"
typeof_expr         = "typeof" "(" type ")"
literal             = "()" | "null" | "true" | "false" | string | int

string              = ...
int                 = ...
identifier          = ...

NL                  = "\n"
INDENT              = { " " }
EOF                 = \EOF
